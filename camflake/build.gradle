/*
 * This build file was generated by the Gradle 'init' task.
 *
 * This generated file contains a sample Java Library project to get you started.
 * For more details take a look at the Java Libraries chapter in the Gradle
 * user guide available at https://docs.gradle.org/3.5/userguide/java_library_plugin.html
 */
plugins {
    id "java-library"
    id "org.sonarqube" version "2.5"
    id "jacoco"
}

group = "com.camobile.camflake"

configurations {
    codacy
}

repositories {
    jcenter()
    maven { url "https://jitpack.io" }
    maven { url "http://dl.bintray.com/typesafe/maven-releases" }
}

dependencies {
    implementation('org.slf4j:slf4j-api:1.7.25')
    testImplementation('org.jmockit:jmockit:1.31')
    testImplementation('junit:junit:4.12')
    codacy('com.github.codacy:codacy-coverage-reporter:2.0.0')
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from "${buildDir}/javadoc"
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

task sendCoverageToCodacy(type: JavaExec, dependsOn: jacocoTestReport) {
    main = "com.codacy.CodacyCoverageReporter"
    classpath = configurations.codacy
    args = [
            "-l",
            "Java",
            "-r",
            "${buildDir}/reports/jacoco/test/jacocoTestReport.xml"
    ]
}

artifacts {
    archives javadocJar, sourcesJar
}

def isDevBuild = true
version += "-SNAPSHOT"

uploadArchives {
    repositories {
        if (isDevBuild) {
            mavenLocal()
        }
    }
}
